# - Config file for the ISSofaPython package
# It defines the following variables
# - ISSofaPython_INCLUDE_DIRS : include directories for ISSofaPython
# - ISSofaPython_LIBRARIES    : libraries to link against

include(CMakeFindDependencyMacro)

list(FIND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}" HAS_ISSOFAPYTHON_CMAKE_MODULE_PATH)

if(HAS_ISSOFAPYTHON_CMAKE_MODULE_PATH EQUAL -1)
    list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

# make the issofapython_add_package method reachable
include(ISSofaPythonSetupTools)

set(ISSOFAPYTHON_USE_PYBIND11_GIL "@ISSOFAPYTHON_USE_PYBIND11_GIL@" CACHE INTERNAL "")
set(ISSOFAPYTHON_CREATE_CHILD_HOOK "@ISSOFAPYTHON_CREATE_CHILD_HOOK@" CACHE INTERNAL "")
set(ISSOFAPYTHON_USE_LEGACY_API "@ISSOFAPYTHON_USE_LEGACY_API@" CACHE INTERNAL "")

if( NOT TARGET SofaSimulationCommon )
    find_package(SofaSimulation REQUIRED)
endif()
if( NOT TARGET pybind11::module )
    find_package(pybind11 REQUIRED)
endif()

if(NOT TARGET ISSofaPython)
    include("${CMAKE_CURRENT_LIST_DIR}/ISSofaPythonTargets.cmake")
endif()

get_property(ISSofaPython_INCLUDE_DIRS TARGET ISSofaPython PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(ISSofaPython_LIBRARIES TARGET ISSofaPython PROPERTY INTERFACE_LINK_LIBRARIES)

set(ISSofaPython_LIBRARIES ISSofaPython ${ISSofaPython_LIBRARIES} CACHE INTERNAL "")
set(ISSofaPython_INCLUDE_DIRS ${ISSofaPython_INCLUDE_DIRS} CACHE INTERNAL "")
